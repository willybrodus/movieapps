apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-parcelize'
apply plugin: 'de.mannodermaus.android-junit5'

apply from: '../shared_dependencies.gradle'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 2

def packageName = "com.dicoding.whatsnewmovie"
def appName = "Whats Movie"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId packageName
        minSdkVersion 21
        targetSdkVersion 30
        versionCode versionMajor * 1000000 + versionMinor * 1000 + versionPatch * 100
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"
        testInstrumentationRunner "com.dicoding.whatsnewmoview.CustomTestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
        exclude "META-INF/LICENSE*"
    }

    flavorDimensions "mode"
    productFlavors {
        dev {
            resValue "string", "app_alias_name", appName
            dimension "mode"
            buildConfigField "String", "BASE_URL", "\"https://api.themoviedb.org/\""
        }
    }

    viewBinding {
        enabled = true
    }

    // Add Kotlin source directory to all source sets
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests {
            includeAndroidResources = true
        }
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
            jacocoOptions {
                html.enabled = true
                xml.enabled = false
                csv.enabled = false
                unitTests.all {
                    testLogging.events = ["passed", "skipped", "failed"]
                }
            }
        }
    }

    hilt {
        enableTransformForLocalTests = true
    }

    lintOptions {
        abortOnError false
    }

    dynamicFeatures = [':myfavoritepage']

}

dependencies {

    //Base Lib
    implementation project(":core")
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api "androidx.navigation:navigation-fragment-ktx:2.3.5"
    api "androidx.navigation:navigation-ui-ktx:2.3.5"
    api "androidx.navigation:navigation-dynamic-features-fragment:2.3.5"

    api "net.zetetic:android-database-sqlcipher:4.4.0"
    api "androidx.sqlite:sqlite-ktx:2.1.0"
    api "androidx.room:room-ktx:2.3.0"
    api "androidx.room:room-rxjava2:2.3.0"
    annotationProcessor "androidx.room:room-compiler:2.3.0"

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.2.2"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.2.2"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    debugImplementation 'androidx.fragment:fragment-testing:1.3.4'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
}